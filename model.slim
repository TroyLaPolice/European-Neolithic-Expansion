initialize()
{
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy", periodicity="");
	
	// ---------------------------------------------------
	//  PARAMETERS --> Initialize Constant Params
	// ---------------------------------------------------
	//SET WORKING DIRECTORY AND CUSTOM MAP NAME IF DESIRED
	if (!exists("wd"))
		defineConstant("wd", "/storage/home/tml5905/work/CoralHybridization");
	if (!exists("custom_map_filename"))
		defineConstant("custom_map_filename", 0); // Parameter = file name of map (as string) if user wants to use their own map and override the built in maps, else == 0
	if (!exists("output_name"))
		defineConstant("output_name", "out"); // To run default names make an empty string
	
	// Carrying Capacities and Pop Sizes:
	// ***********************************
	if (!exists("DS"))
		defineConstant("DS", 1); // Down scale the simulation ( Must be >= 1)
	if (!exists("SN"))
		defineConstant("SN", integerDiv(4500, DS)); // Starting number of individuals
	if (!exists("cervicornisK"))
		defineConstant("cervicornisK", 1500 / DS); // carrying capacity (ENTER IN INDIVIDUALS PER KM2) for density dependent scaling 
	if (!exists("palmataK"))
		defineConstant("palmataK", 1500 / DS); // carrying capacity for palmata (ENTER IN INDIVIDUALS PER KM2) for density dependent scaling
	if (!exists("proliferaK"))
		defineConstant("proliferaK", 1500 / DS); // carrying capacity for palmata (ENTER IN INDIVIDUALS PER KM2) for density dependent scaling
	if (!exists("initial_ratio"))
		defineConstant("initial_ratio", 0.5); // percentage of inital population that is palmata (used in initilization step of sim)
	
	// Movement and interaction and competition:
	// ***********************************
	if (!exists("SDX"))
		defineConstant("SDX", 10); // Movement standard diviation (sigma) for distribution of distances sampled from in the x direction (ENTER IN KILOMETERS)
	if (!exists("SDY"))
		defineConstant("SDY", 10); // Movement standard diviation (sigma) for distribution of distances sampled from in the y direction (ENTER IN KILOMETERS)
	
	// Mating rate params:
	// ***********************************
	if (!exists("settle_cer"))
		defineConstant("settle_cer", 0.022308); // Settlement for cervicornis X cervicornis cross
	if (!exists("settle_pal"))
		defineConstant("settle_pal", 0.0351); // Settlement for palmata X palmata cross
	if (!exists("settle_pro"))
		defineConstant("settle_pro", 0.07215); // Settlement for a prolifera
	if (!exists("MD"))
		defineConstant("MD", 5); // Mating distance (ENTER IN KILOMETERS)
	if (!exists("cer_cer"))
		defineConstant("cer_cer", 0.525); // Mating pref for cervicornis X cervicornis
	if (!exists("pal_pal"))
		defineConstant("pal_pal", 0.705); // Mating pref for palmata X palmata
	if (!exists("pro_pro"))
		defineConstant("pro_pro", 0.5); // Mating pref for prolifera X prolifera
	
	// ---------------------------------------------------
	// RUN TIME PREFERENCE PARAMETERS
	// ---------------------------------------------------
	
	// Determine Coloring Schemes:
	// ***********************************	
	defineConstant("Color_scheme", 1); // Parameter of 0 = Genomic Coloring, 1 = Phenotypic Coloring
	
	// Map Prefs: 
	// ***********************************	
	if (!exists("map_style"))
		defineConstant("map_style", 0); // 0 = simple, 1 = Laughing Bird Caye, 6 = custom map
	defineConstant("file_extention", ".png");
	defineConstant("simple", wd + "/simple.png"); // File Path to Map Image
	defineConstant("LaughingBirdCaye", wd + "/laughing_bird_caye.png"); // File Path to Map Image
	// Map citation: https://www.laughingbird.org/park2.html
	defineConstant("custom_map", wd + custom_map_filename); // File Path to Map Image
	
	// Map Dimensions of 8.96km x 12.964km Based on laughingbird.org map
	if (!exists("map_size_length"))
		defineConstant("map_size_length", 12.964);
	if (!exists("map_size_width"))
		defineConstant("map_size_width", 8.96);
	
	// ----------------------------------------------------
	//  GENETIC COMPONENT --> Initialize Genomic Elements
	// ----------------------------------------------------
	initializeMutationType("m1", 0.5, "f", 0.0); // Tag palmata ancestry
	m1.convertToSubstitution = F;
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 267); // Genome Size in MB
	
	initializeMutationType("m2", 0.5, "f", 0.0); // Speciation Loci
	m2.convertToSubstitution = F;
	
	initializeMutationRate(0.0); // Zero Mutations
	rates = c(0.0447, 0.5, 0.0359, 0.5, 0.05, 0.5, 0.0453, 0.5, 0.05, 0.5, 0.0419, 0.5, 0.0505, 0.5, 0.0469, 0.5, 0.0424, 0.5, 0.0518, 0.5, 0.0542, 0.5, 0.0501, 0.5, 0.041, 0.5, 0.0632);
	ends = c(27, 28, 54, 55, 76, 77, 95, 96, 115, 116, 136, 137, 152, 153, 167, 168, 186, 187, 205, 206, 222, 223, 239, 240, 253, 254, 267); // Recombination Rates and Chromosome Initialization
	initializeRecombinationRate(rates, ends);
	
	// ---------------------------------------------------
	//  INTERACTIONS --> Interaction Initialization
	// ---------------------------------------------------
	// spatial mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=MD);
}

1 early()
{
	sim.addSubpop("p1", SN); // Initialize population based on starting number of individuals
}

2: first()
{
	i2.evaluate(p1); // Initialize mating interaction
}

1 early()
{
	
	// -----------------------------------------------------------------------------
	//  Before sim starts ---> Initalize map and create initial starting individuals
	// -----------------------------------------------------------------------------
	
	// Check user input for what style map
	if (map_style == 0)
		mapImage = Image(simple); //simple
	else if (map_style == 1)
		mapImage = Image(LaughingBirdCaye); //caye
	else if (map_style == 2)
		mapImage = Image(custom_map); //custom
	
	// Set up map
	p1.setSpatialBounds(c(0.0, 0.0, map_size_width, map_size_length));
	p1.defineSpatialMap("map_object", "xy", 1.0 - mapImage.floatK, valueRange=c(0.0, 1.0), colors=c("#ffffff", "#000000"));
	
	// place individuals on the map
	for (ind in p1.individuals)
	{
		do
			newPos = c(runif(1, 0, map_size_width), runif(1, 0, map_size_length));
		while (!p1.pointInBounds(newPos) | p1.spatialMapValue("map_object", newPos) == 0.0);
		ind.setSpatialPosition(newPos);
		ind.z = 0;
	}
	
	// Define z param in individuals (phenotype, 0 = cervicornis, 1 = palmata, 2 = prolifera)
	palmata_ind = p1.sampleIndividuals(asInteger(initial_ratio*SN));
	palmata_ind.z = 1;
	
	// Tag genomic ancestry of palmata with marker mutations (m1)
	// Each marker mutation represents 1Mb
	indpalmata = p1.individuals[p1.individuals.z == 1];
	indpalmata.genomes.addNewMutation(m1, 0.0, 0:267);
	
	// Add speciation loci
	indpalmata.genomes.addNewMutation(m2, 0.0, 0:0);
	indpalmata.genomes.addNewMutation(m2, 0.0, 3:3);
	indpalmata.genomes.addNewMutation(m2, 0.0, 6:6);
	indpalmata.genomes.addNewMutation(m2, 0.0, 9:9);
	indpalmata.genomes.addNewMutation(m2, 0.0, 12:12);
	indpalmata.genomes.addNewMutation(m2, 0.0, 15:15);
	indpalmata.genomes.addNewMutation(m2, 0.0, 18:18);
	indpalmata.genomes.addNewMutation(m2, 0.0, 21:21);
	indpalmata.genomes.addNewMutation(m2, 0.0, 24:24);
	indpalmata.genomes.addNewMutation(m2, 0.0, 27:27);
	indpalmata.genomes.addNewMutation(m2, 0.0, 30:30);
	indpalmata.genomes.addNewMutation(m2, 0.0, 33:33);
	indpalmata.genomes.addNewMutation(m2, 0.0, 36:36);
	indpalmata.genomes.addNewMutation(m2, 0.0, 39:39);
	indpalmata.genomes.addNewMutation(m2, 0.0, 42:42);
	indpalmata.genomes.addNewMutation(m2, 0.0, 45:45);
	indpalmata.genomes.addNewMutation(m2, 0.0, 48:48);
	indpalmata.genomes.addNewMutation(m2, 0.0, 51:51);
	indpalmata.genomes.addNewMutation(m2, 0.0, 54:54);
	indpalmata.genomes.addNewMutation(m2, 0.0, 57:57);
	indpalmata.genomes.addNewMutation(m2, 0.0, 60:60);
	indpalmata.genomes.addNewMutation(m2, 0.0, 63:63);
	indpalmata.genomes.addNewMutation(m2, 0.0, 66:66);
	indpalmata.genomes.addNewMutation(m2, 0.0, 69:69);
	indpalmata.genomes.addNewMutation(m2, 0.0, 72:72);
	indpalmata.genomes.addNewMutation(m2, 0.0, 75:75);
	indpalmata.genomes.addNewMutation(m2, 0.0, 78:78);
	indpalmata.genomes.addNewMutation(m2, 0.0, 81:81);
	indpalmata.genomes.addNewMutation(m2, 0.0, 84:84);
	indpalmata.genomes.addNewMutation(m2, 0.0, 87:87);
	indpalmata.genomes.addNewMutation(m2, 0.0, 90:90);
	indpalmata.genomes.addNewMutation(m2, 0.0, 93:93);
	indpalmata.genomes.addNewMutation(m2, 0.0, 96:96);
	indpalmata.genomes.addNewMutation(m2, 0.0, 99:99);
	indpalmata.genomes.addNewMutation(m2, 0.0, 102:102);
	indpalmata.genomes.addNewMutation(m2, 0.0, 105:105);
	indpalmata.genomes.addNewMutation(m2, 0.0, 108:108);
	indpalmata.genomes.addNewMutation(m2, 0.0, 111:111);
	indpalmata.genomes.addNewMutation(m2, 0.0, 114:114);
	indpalmata.genomes.addNewMutation(m2, 0.0, 117:117);
	indpalmata.genomes.addNewMutation(m2, 0.0, 120:120);
	indpalmata.genomes.addNewMutation(m2, 0.0, 123:123);
	indpalmata.genomes.addNewMutation(m2, 0.0, 126:126);
	indpalmata.genomes.addNewMutation(m2, 0.0, 129:129);
	indpalmata.genomes.addNewMutation(m2, 0.0, 132:132);
	indpalmata.genomes.addNewMutation(m2, 0.0, 135:135);
	indpalmata.genomes.addNewMutation(m2, 0.0, 138:138);
	indpalmata.genomes.addNewMutation(m2, 0.0, 141:141);
	indpalmata.genomes.addNewMutation(m2, 0.0, 144:144);
	indpalmata.genomes.addNewMutation(m2, 0.0, 147:147);
	indpalmata.genomes.addNewMutation(m2, 0.0, 150:150);
	indpalmata.genomes.addNewMutation(m2, 0.0, 153:153);
	indpalmata.genomes.addNewMutation(m2, 0.0, 156:156);
	indpalmata.genomes.addNewMutation(m2, 0.0, 159:159);
	indpalmata.genomes.addNewMutation(m2, 0.0, 162:162);
	indpalmata.genomes.addNewMutation(m2, 0.0, 165:165);
	indpalmata.genomes.addNewMutation(m2, 0.0, 168:168);
	indpalmata.genomes.addNewMutation(m2, 0.0, 171:171);
	indpalmata.genomes.addNewMutation(m2, 0.0, 174:174);
	indpalmata.genomes.addNewMutation(m2, 0.0, 177:177);
	indpalmata.genomes.addNewMutation(m2, 0.0, 180:180);
	indpalmata.genomes.addNewMutation(m2, 0.0, 183:183);
	indpalmata.genomes.addNewMutation(m2, 0.0, 186:186);
	indpalmata.genomes.addNewMutation(m2, 0.0, 189:189);
	indpalmata.genomes.addNewMutation(m2, 0.0, 192:192);
	indpalmata.genomes.addNewMutation(m2, 0.0, 195:195);
	indpalmata.genomes.addNewMutation(m2, 0.0, 198:198);
	indpalmata.genomes.addNewMutation(m2, 0.0, 201:201);
	indpalmata.genomes.addNewMutation(m2, 0.0, 204:204);
	indpalmata.genomes.addNewMutation(m2, 0.0, 207:207);
	indpalmata.genomes.addNewMutation(m2, 0.0, 210:210);
	indpalmata.genomes.addNewMutation(m2, 0.0, 213:213);
	indpalmata.genomes.addNewMutation(m2, 0.0, 216:216);
	indpalmata.genomes.addNewMutation(m2, 0.0, 219:219);
	indpalmata.genomes.addNewMutation(m2, 0.0, 222:222);
	indpalmata.genomes.addNewMutation(m2, 0.0, 225:225);
	indpalmata.genomes.addNewMutation(m2, 0.0, 228:228);
	indpalmata.genomes.addNewMutation(m2, 0.0, 231:231);
	indpalmata.genomes.addNewMutation(m2, 0.0, 234:234);
	indpalmata.genomes.addNewMutation(m2, 0.0, 237:237);
	indpalmata.genomes.addNewMutation(m2, 0.0, 240:240);
	indpalmata.genomes.addNewMutation(m2, 0.0, 243:243);
	indpalmata.genomes.addNewMutation(m2, 0.0, 246:246);
	indpalmata.genomes.addNewMutation(m2, 0.0, 249:249);
	indpalmata.genomes.addNewMutation(m2, 0.0, 252:252);
	indpalmata.genomes.addNewMutation(m2, 0.0, 255:255);
	indpalmata.genomes.addNewMutation(m2, 0.0, 258:258);
	indpalmata.genomes.addNewMutation(m2, 0.0, 261:261);
	indpalmata.genomes.addNewMutation(m2, 0.0, 264:264);
	indpalmata.genomes.addNewMutation(m2, 0.0, 267:267);
	
	
	// Add color to represent phenotype
	for (i in p1.individuals)
	{
		if (Color_scheme == 1)
		{
			// --------------------------------
			// Color Based on Discrete Species
			// --------------------------------
			
			// Add color to represent phenotype
			if (i.z == 0) // cervicornis
				i.color = "red"; //cervicornis = red
			else if (i.z == 1) // palmata
				i.color = "blue"; //palmata = blue
			else if (i.z == 2) // prolifera
				i.color = "green"; // prolifera = green
		}
		else
		{
			// -------------------------------
			// Color Based on Percent Ancestry
			// -------------------------------
			
			ancestry = i.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2);
			i.color = rgb2color(hsv2rgb(c(0.6, 1.0, ancestry)));
		}
	}
}

1 early()
{
	
	// -----------------------------------------------
	//  Before sim starts ---> Initialize output files
	// -----------------------------------------------
	
	// log runtime params
	param_string = paste(SN, cervicornisK, palmataK, proliferaK, SDX, SDY, initial_ratio);
	
	// Runtime params file headings - write to file
	param_heading = paste("SN cervicornisK palmataK proliferaK SDX SDY initial_ratio");
	
	// Runtime params - write to file
	output_runtime_file_name = ("/sim_runtime_params_" + output_name + ".tsv");
	writeFile(wd + output_runtime_file_name, param_heading, append=T);
	writeFile(wd + output_runtime_file_name, param_string, append=T);
	
	// Population stats file headings - write to file
	stats_header_string = paste("Year", "PopulationSize", "Total_palmata", "Total_cervicornis", "Total_prolifera", "Ratio_palmata_to_cervicornis", "cervicornis_ancestry_in_palmata", "palmata_ancestry_in_cervicornis", "palmata_ancestry_in_prolifera", "cervicornis_ancestry_in_prolifera" ,sep=",");
	output_stats_file_name = ("/sim_pop_stats_per_year_" + output_name + ".csv");
	writeFile(wd + output_stats_file_name, stats_header_string, append=T);
}

reproduction()
{
	// ---------------------------------------------------
	//  MATING --> Individuals mate with those close by
	// ---------------------------------------------------
	
	// creates a vector of neighboring individuals, within the max distance
	mates = i2.nearestInteractingNeighbors(individual, p1.individuals.length());
	
	if (mates.size()) // Runs if there are possible mates near by
	{
		// Sets up a vector that checks if the individuals in the surrounding area are of like phenotype (i.e. if their z params are the same)
		mates_similarity_bool = mates.z == individual.z;
		mates_similarity = asFloat(mates_similarity_bool); // convert to float to allow for mutation of the vector (replace with probabilities of type float)
		
		// Generates a vector of probabilites of the individual mating with a particular mate given their phenotype
		
		if (individual.z == 0) // individual == cervicornis
		{
			mates_similarity[mates_similarity_bool == T] = cer_cer; // selection probability for mates that are also cervicornis
			mates_similarity[mates_similarity_bool == F] = (1 - cer_cer); // selection probability for mates that are not cervicornis
		}
		else if (individual.z == 1) // individual == palmata
		{
			mates_similarity[mates_similarity_bool == T] = pal_pal; // selection probability for mates that are also palmata
			mates_similarity[mates_similarity_bool == F] = (1 - pal_pal); // selection probability for mates that are not palmata
		}
		else if (individual.z == 2) // individual == prolifera
		{
			mates_similarity[mates_similarity_bool == T] = pro_pro; // selection probability for mates that are also prolifera
			mates_similarity[mates_similarity_bool == F] = (1 - pro_pro); // selection probability for mates would result in a backcross
		}
		
		if (sum(mates_similarity) != 0)
		{
			// Samples an individual in the population based on the assortative mating probabilities. 
			mate = sample(mates, 1, weights=mates_similarity);
			
			// select proper settlement rate:
			// ------------------------------
			
			if (individual.z == 0) // if individual is cervicornis
			{
				if (mate.z == 0) // if mate is cervicornis
					settle = settle_cer; // settle at cervicornis rate
				else if (mate.z == 1) // if mate is palmata
					settle = settle_pro;  // settle at prolifera rate
				else if (mate.z == 2) // if mate is prolifera
					settle = settle_cer; // settle at cervicornis rate
			}
			else if (individual.z == 1) // if individual is palmata
			{
				if (mate.z == 0) // if mate is cervicornis
					settle = settle_pro; // settle at prolifera rate
				else if (mate.z == 1) // if mate is palmata
					settle = settle_pal; // settle at palmata rate 
				else if (mate.z == 2) // if mate is prolifera
					settle = settle_pal; // settle at palmata rate
			}
			else if (individual.z == 2) // if individual is prolifera
			{
				if (mate.z == 0) // if mate is cervicornis
					settle = settle_cer; // settle at cervicornis rate
				else if (mate.z == 1) // if mate is palmata
					settle = settle_pal; // settle at palmata rate
				else if (mate.z == 2) // if mate is prolifera
					settle = settle_pro; // settle at prolifera rate
			}
			
			// Generate offspring that will settle sucessfully based on above rate determination
			for (i in seqLen(rpois(1, settle)))
			{
				// Only runs if a mate is nearby
				if (mate.size())
				{
					// Generates an offspring
					offspring = subpop.addCrossed(individual, mate);
					
					// Define z param in offspring (phenotype, 0 = cervicornis, 1 = palmata, 2 == prolifera)
					offspring.z = 0;
					
					// Determine percent ancestry in offspring
					ancestry = offspring.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2);
					
					// Determine the genotype at the speciation loci
					species_loci = offspring.countOfMutationsOfType(m2);
					
					print(species_loci);
					
					// Determine species of the offspring based on ancestry threshold
					
					if (species_loci >= 162)
						offspring.z = 1; // offspring is palmata
					else if (species_loci <= 18)
						offspring.z = 0; // offspring is cervicornis
					else
						offspring.z = 2;  // offspring is prolifera
					
					if (Color_scheme == 1) // Coloring in GUI of individuals
					{
						// --------------------------------
						// Color Based on Discrete Species
						// --------------------------------
						
						// Add color to represent phenotype
						if (offspring.z == 0) // cervicornis
							offspring.color = "red"; //cervicornis = red
						else if (offspring.z == 1) // palmata
							offspring.color = "blue"; //palmata = blue
						else if (offspring.z == 2) // prolifera
							offspring.color = "green"; // prolifera = green
					}
					else
					{
						// -------------------------------
						// Color Based on Percent Ancestry
						// -------------------------------
						
						// Calculate percent ancestry
						ancestry = offspring.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2);
						
						// Color as a gradient based on the percent ancestry
						offspring.color = rgb2color(hsv2rgb(c(0.6, 1.0, ancestry)));
					}
					
					// Determine an intital offspring postion based to initalize the individual before dispersal step below
					pos = individual.spatialPosition;
					offspring.setSpatialPosition(pos);
					
					// ---------------------------------------------------
					//  MOVEMENT --> How larvae disperse
					// ---------------------------------------------------
					
					// How far individuals diffuse away
					do
					{
						// Normal multivariate distribution dictating dispersal to pick new coordinates in the movement range
						coordinate_matrix = rmvnorm(1, c(0, 0), matrix(c(SDX^2, 0, 0, SDY^2), nrow=2));
						coordinates = c(coordinate_matrix[0], coordinate_matrix[1]);
						
						// Determine the new positional coordinates
						newPos = offspring.spatialPosition + coordinates;
					}
					while (!p1.pointInBounds(newPos) | p1.spatialMapValue("map_object", newPos) == 0.0); // While not bounds
					
					// Set settle point for offspring
					offspring.setSpatialPosition(newPos);
				
				}
			}
		}
	}
}

late()
{
	// --------------------------------------------------------------------------------
	//  MORTALITY ---> Density dependent competion avoids exponential population growth
	// --------------------------------------------------------------------------------
	
	// Initalize vectors of the species
	cervicornis_inds = p1.individuals[p1.individuals.z == 0];
	palmata_inds = p1.individuals[p1.individuals.z == 1];
	prolifera_inds = p1.individuals[p1.individuals.z == 2];
	
	// Scale fitness based on population densities
	cervicornis_inds.fitnessScaling = cervicornisK / (length(cervicornis_inds));
	palmata_inds.fitnessScaling = palmataK / (length(palmata_inds));
	prolifera_inds.fitnessScaling = proliferaK / (length(prolifera_inds));
}

1:6000 late()
{
	
	// -------------------------------------------
	//  Each Generation ---> Output stats to files
	// -------------------------------------------
	
	// provide feedback on progress for command line users
	year_counter = paste("Simulation Year: ", sim.cycle);
	print(year_counter);
	
	// define vector of palmata, vector of cervicornis, vector of prolifera
	palmata = p1.individuals[p1.individuals.z == 1];
	cervicornis = p1.individuals[p1.individuals.z == 0];
	prolifera = p1.individuals[p1.individuals.z == 2];
	
	// calculate num palmata
	num_palmata = length(palmata);
	
	//calculate num cervicornis
	num_cervicornis = length(cervicornis);
	
	//calculate num prolifera
	num_prolifera = length(prolifera);
	
	// calculate the ratio of palmata in the total population to file
	ratio = (num_palmata / num_cervicornis);
	
	// calculate population size statistics
	pop_size = p1.individuals.length();
	
	// calculate the cervicornis ancestry in all palmata
	if (length(palmata) != 0)
		cervicornis_ancestry_palmata = (1 - (mean(palmata.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2))));
	else
		palmata_ancestry_palmata = 0.0;
	
	// calculate the palmata ancestry in all cervicornis
	if (length(cervicornis) != 0)
		palmata_ancestry_cervicornis = mean(cervicornis.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2));
	else
		palmata_ancestry_cervicornis = 0.0;
	
	// calculate the palmata ancestry in all prolifera
	if (length(prolifera) != 0)
		palmata_ancestry_prolifera = mean(prolifera.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2));
	else
		palmata_ancestry_prolifera = 0.0;
	
	// calculate the cervicornis ancestry in all prolifera
	if (length(prolifera) != 0)
		cervicornis_ancestry_prolifera = (1 - (mean(prolifera.countOfMutationsOfType(m1) / (sim.chromosome.lastPosition * 2))));
	else
		cervicornis_ancestry_prolifera = 0.0;
	
	// write outputs
	output_string = paste(sim.cycle, pop_size, num_palmata, num_cervicornis, num_prolifera, ratio, cervicornis_ancestry_palmata, palmata_ancestry_cervicornis, palmata_ancestry_prolifera, cervicornis_ancestry_prolifera, sep=",");
	output_stats_file_name = ("/sim_pop_stats_per_year_" + output_name + ".csv");
	writeFile(wd + output_stats_file_name, output_string, append=T);
}
